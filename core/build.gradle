bootJar { enabled = false }
jar { enabled = true }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly("com.mysql:mysql-connector-j")

    // querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}


// ### QueryDSL 관련 설정 시작 ###
// Querydsl Q Class 생성 위치
def querydslDir = 'src/generated/querydsl'

// java source set 에 Querydsl Q Class 위치 추가
sourceSets {
    main.java.srcDirs += [querydslDir]
}

// 생성된 Q Class가 컴파일될 디렉토리 설정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

// gradle clean 시, Q Class가 위치한 디렉토리 삭제
clean.doLast {
    file(querydslDir).deleteDir()
}
// ### QueryDSL 관련 설정 끝 ###